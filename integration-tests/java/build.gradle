/*
 * Copyright 2019 Infostellar, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/main/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
}


task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'junit-vintage'
    }

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

dependencies {
    // You will need to replace the dependency to stellarstation-api with
    // implementation 'com.stellarstation.api:stellarstation-api:0.x.x' in your project.
    implementation project(':api')

    implementation 'com.google.auth:google-auth-library-oauth2-http:0.16.1'
    implementation 'com.google.dagger:dagger:2.23.1'
    implementation 'com.google.protobuf:protobuf-java-util:3.8.0'
    implementation 'com.typesafe:config:1.3.4'
    ['auth', 'netty', 'protobuf', 'stub'].each {
        implementation "io.grpc:grpc-$it:1.21.0"
    }
    implementation 'io.netty:netty-tcnative-boringssl-static:2.0.25.Final'
    implementation 'org.slf4j:slf4j-simple:1.7.26'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.23.1'
    integrationTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.23.1'

    integrationTestImplementation 'com.typesafe:config:1.3.4'
    integrationTestImplementation 'org.assertj:assertj-core:3.11.1'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}
