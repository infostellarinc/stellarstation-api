/*
 * Copyright 2018 Infostellar, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.curioswitch.gradle-grpc-api-plugin'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'stellarstation-api'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'StellarStation API'
                description = 'Java stubs and protocol definition for accessing the StellarStation API.'
                url = 'https://github.com/infostellarinc/stellarstation-api'
            }
        }
    }
    repositories {
        maven {
            if (version.startsWith('0.0.0')) {
                // snapshot
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                // release
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            credentials {
                username = rootProject.findProperty('maven.user')
                password = rootProject.findProperty('maven.key')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    keyId = rootProject.findProperty('sign.keyId')
    password = rootProject.findProperty('sign.password')
    secretKeyRingFile = rootProject.findProperty('sign.keyFile')
}

afterEvaluate {
    if (version.startsWith('0.0.0')) {
        version = version + '-SNAPSHOT'
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
