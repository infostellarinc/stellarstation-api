/*
 * Copyright 2018 Infostellar, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.curioswitch.gradle-grpc-api-plugin'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

archivesBaseName = 'stellarstation-api'

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'StellarStation API'
                description = 'Java stubs and protocol definition for accessing the StellarStation API.'
                url = 'https://github.com/infostellarinc/stellarstation-api'
            }
        }
    }
    repositories {
        maven {
            if (version.startsWith('0.0.0')) {
                // snapshot
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                // release
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            credentials {
                username = rootProject.findProperty('maven.username')
                password = rootProject.findProperty('maven.password')
            }
        }
    }
}

signing {
    // Need to set the following project properties
    //   signing.keyId=24875D73 // last 8 characters of key ID
    //   signing.password=secret // secret key passphrase
    //   signing.secretKeyRingFile=/home/user/.gnupg/secring.gpg // path to secret keyring in `gpg` format
    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
    sign publishing.publications.maven
}

afterEvaluate {
    if (version.startsWith('0.0.0')) {
        version = version + '-SNAPSHOT'
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
