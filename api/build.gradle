/*
 * Copyright 2018 Infostellar, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'org.curioswitch.gradle-grpc-api-plugin'
    id 'maven-publish'
}

archivesBaseName = 'stellarstation-api'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'StellarStation API'
                description = 'Java stubs and protocol definition for accessing the StellarStation API.'
                url = 'https://github.com/infostellarinc/stellarstation-api'
            }
        }
    }
}

protobuf {
    protoc {
        artifact.set("com.google.protobuf:protoc:3.11.1")
    }
}

javadoc {
    source 'build/generated/proto/main'
}

sourcesJar {
    from 'build/generated/proto/main'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = rootProject.findProperty('bintray.user')
            password = rootProject.findProperty('bintray.key')
        }
        defaults {
            publications 'maven'
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}

afterEvaluate {
    if (version.startsWith('0.0.0')) {
        version = version + '-SNAPSHOT'
    }
}
